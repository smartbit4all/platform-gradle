plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
    id 'java-gradle-plugin'

    // Apply the Groovy plugin to add support for Groovy
    id 'groovy'

    // publish
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.15.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation 'io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE'
    implementation 'org.springframework.boot:spring-boot-gradle-plugin:2.3.12.RELEASE'
    implementation 'org.openapitools:openapi-generator-gradle-plugin:5.3.0'

    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

group = 'org.smartbit4all.platform.gradle'
version = '0.3.5'

pluginBundle {
    website = 'https://github.com/smartbit4all'
    vcsUrl = 'https://github.com/smartbit4all/platform-gradle.git'
    tags = ['smartbit4all', 'java', 'vaadin', 'openapi']
}

gradlePlugin {
    // Define the plugin
    plugins {
        sb4java {
            id = 'org.smartbit4all.platform.gradle.java'
            displayName = 'smartbit4all Java Gradle plugin'
            description = 'Generic plugin for use with smartbit4all based projects, adds Java dependencies.'
            implementationClass = 'org.smartbit4all.platform.gradle.SB4JavaPlugin'
        }
        sb4vaadinmodule {
            id = 'org.smartbit4all.platform.gradle.vaadin-module'
            displayName = 'smartbit4all Vaadin module Gradle plugin'
            description = 'Generic plugin for use with smartbit4all based projects, adds Vaadin dependencies for module development.'
            implementationClass = 'org.smartbit4all.platform.gradle.SB4VaadinModulePlugin'
        }
        sb4vaadinapp {
            id = 'org.smartbit4all.platform.gradle.vaadin-app'
            displayName = 'smartbit4all Java Gradle plugin'
            description = 'Generic plugin for use with smartbit4all based projects, adds Vaadin dependencies for application development.'
            implementationClass = 'org.smartbit4all.platform.gradle.SB4VaadinAppPlugin'
        }
        sb4openapi {
            id = 'org.smartbit4all.platform.gradle.openapi'
            displayName = 'smartbit4all Java Gradle plugin'
            description = 'Generic plugin for use with smartbit4all based projects, adds openApiGenerate<API> targets for OpenApi generation.'
            implementationClass = 'org.smartbit4all.platform.gradle.SB4OpenApiPlugin'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

//publishing {
//    repositories {
//        maven {
//            url 'https://pkgs.dev.azure.com/it4all/develop/_packaging/it4all-artifacts/maven/v1'
//            name 'it4allArtifacts'
//            credentials(PasswordCredentials)
//            authentication {
//                basic(BasicAuthentication)
//            }
//        }
//    }
//}
